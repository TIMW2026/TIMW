pkgname=vkd3d-proton-mingw-git
pkgver=2.14.1.r336.gb72e62ca
pkgrel=1
arch=(x86_64)
depends=(vulkan-icd-loader wine bash)
makedepends=(ninja meson glslang git mingw-w64-gcc mingw-w64-tools)
source=(git+https://github.com/HansKristian-Work/vkd3d-proton.git setup_vkd3d_proton.sh setup_vkd3d_proton)

pkgver() {
    cd vkd3d-proton
    printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/v//g')"
}

prepare() {
    cd vkd3d-proton
    git remote set-url origin https://github.com/HansKristian-Work/vkd3d-proton.git
    git submodule update --init --filter=tree:0 --recursive
}

build() {
    LDFLAGS=${LDFLAGS/-fuse-ld=lld}
    meson setup vkd3d-proton "build/x64" \
        --prefix "/usr/share/vkd3d-proton/x64" \
        --cross-file vkd3d-proton/build-win64.txt \
        --bindir "" --libdir "" \
        --buildtype "release" \
        -Denable_tests=false \
        -Db_ndebug=false \
        --strip
    ninja -C "build/x64" -v
    meson setup vkd3d-proton "build/x86" \
        --cross-file vkd3d-proton/build-win32.txt \
        --prefix "/usr/share/vkd3d-proton/x86" \
        --bindir "" --libdir "" \
        --buildtype "release" \
        -Denable_tests=false \
        -Db_ndebug=false \
        --strip
    ninja -C "build/x86" -v
}

package() {
    provides=(vkd3d-proton)
    conflicts=(vkd3d-proton)
    DESTDIR="$pkgdir" ninja -C "build/x86" install
    DESTDIR="$pkgdir" ninja -C "build/x64" install
    install -Dm 755 -t "$pkgdir/usr/share/vkd3d-proton" setup_vkd3d_proton.sh
    install -Dm 755 -t "$pkgdir/usr/bin" setup_vkd3d_proton
}
